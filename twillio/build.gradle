apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def versionCodeProperty = project.property('versionCode') as Integer
def includeSdkFromSourceProperty = 'includeSdkFromSource'
def includeSdkFromSource = project.hasProperty(includeSdkFromSourceProperty) &&
        project.property(includeSdkFromSourceProperty) as Boolean

afterEvaluate {
    def ndkVersion = 'ndkVersion'
    if (includeSdkFromSource && project.hasProperty(ndkVersion)) {
        android.ndkVersion = "${project.property(ndkVersion)}"
    }

    /*
    * Enable the application to consume the Video Android SDK directly from source
    * instead of from Bintray.
    *
    * The property is not defined by default when consuming the
    * application as a stand-alone project, but is defined by default in the Video Android SDK project.
    * This property enables the Video Android SDK project to build the application as a git subtree
    * with the contents of the source itself as a dependency. This coupling is intentional as it allows
    * SDK developers to iterate on SDK features within the application that is maintained in a separate
    * project.
    */
    dependencies {
        if (includeSdkFromSource) {
            debugImplementation project(':video')
            debugImplementation project(':video:ktx')
            releaseImplementation project(':video:ktx')
        } else {
            implementation "com.twilio:video-android-ktx:6.3.0"
        }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        disable 'GradleDependency'
        warningsAsErrors true
        // TODO: Fix all baseline app warnings and errors GSDK-1984
        baseline file("lint-baseline.xml")
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    dataBinding {
        enabled = true
    }

    defaultConfig {

        minSdkVersion 21
        targetSdkVersion 30

        versionName "0.1.0"
        versionCode versionCodeProperty

        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests {
            includeAndroidResources = true
        }
    }



    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-debug"
        }
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    def daggerVersion = '2.28.3'
    def daggerAndroidProcessor = "com.google.dagger:dagger-android-processor:$daggerVersion"
    def daggerCompiler = "com.google.dagger:dagger-compiler:$daggerVersion"
    def retrofitVersion = '2.9.0'
    def espressoVersion = '3.3.0'
    def espresso = "androidx.test.espresso:espresso-core:$espressoVersion"
    def androidXTest = '1.3.0'
    def testCore = "androidx.test:core:$androidXTest"
    def junitExtensions = 'androidx.test.ext:junit-ktx:1.1.1'
    def lifecycleVersion = '2.3.1'
    def coroutinesAndroidVersion = '1.4.3'
    def fragmentVersion = '1.3.2'
    def uniflowVersion = '1.0.5'

    implementation 'com.facebook.conceal:conceal:2.0.2@aar'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesAndroidVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesAndroidVersion"
    // TODO Remove as part of https://issues.corp.twilio.com/browse/AHOYAPPS-445
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutinesAndroidVersion"
    implementation 'com.twilio:twilio-android-env:1.1.0@aar'
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation 'com.google.android.material:material:1.3.0'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    api 'com.jakewharton.timber:timber:4.7.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "com.google.firebase:firebase-core:17.5.0"
    implementation 'com.google.firebase:firebase-crashlytics:17.4.1'
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    implementation 'com.firebaseui:firebase-ui-auth:6.4.0'
    implementation "com.github.denizavsar:DraggableLayout:5.1.1"
    implementation "com.daimajia.swipelayout:library:1.2.0@aar"

    api "com.google.dagger:dagger-android:$daggerVersion"
    api "com.google.dagger:dagger:$daggerVersion"
    api "com.google.dagger:dagger-android-support:$daggerVersion"
    implementation "io.reactivex.rxjava2:rxjava:2.2.10"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.twilio:audioswitch:1.1.2'
    implementation "org.uniflow-kt:uniflow-android:$uniflowVersion"
    api 'com.github.timigod:android-chat-ui:v0.1.4'
    implementation 'com.github.andrefrsousa:SuperBottomSheet:2.0.0'

    api 'com.github.javiersantos:BottomDialogs:1.2.1'
    implementation 'com.gdacciaro:iosdialog:1.0.3'
    implementation 'com.github.shivthepro:stickydragview:0.0.1'

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    /*
     * Include as a compile time only dependency so Android Studio can resolve references
     * to com.android.ViewBinding.
     */
    compileOnly 'com.android.databinding:viewbinding:4.1.3'


    kapt daggerAndroidProcessor
    kapt daggerCompiler


    kaptTest daggerAndroidProcessor
    kaptTest daggerCompiler

}

def getLocalProperty(key) {
    if (project.rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def value = properties.getProperty(key)
        return value
    } else {
        logger.log(LogLevel.WARN, "Could not find local.properties in " +
                project.rootProject.toString() + ". Community variant cannot connect to room" +
                " without credentials")
    }
}
